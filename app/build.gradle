apply plugin: 'com.android.application'

loadConfig()

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId config.getProperty("applicationId")
        minSdkVersion 9
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                applicationName: config.getProperty("applicationName")
        ]
    }
    signingConfigs {
        release {
            storeFile file(config.getProperty("signingConfigs.release.storeFile", "file.jks"))
            storePassword config.getProperty("signingConfigs.release.storePassword")
            keyAlias config.getProperty("signingConfigs.release.keyAlias")
            keyPassword config.getProperty("signingConfigs.release.keyPassword")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.4.0'
    testCompile 'junit:junit:4.12'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
}

def loadConfig() {
    def config = new Properties()
    file("config.properties").withInputStream { config.load(it) }
    project.ext.config = config;
}